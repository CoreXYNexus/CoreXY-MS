///////////////////////////////////////////////////////////////////////////////////////////////////////////
// PTFEBracket.scad
//////////////////////////////////////////////////////////////////////////////////////////////////////////
// Created 11/19/20
// Last Update 11/19/20
///////////////////////////////////////////////////////////////////////////////////////////////////////////
$fn=100;
include <inc/cubex.scad>
include <inc/brassinserts.scad>
include <bosl2/std.scad>
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
Thickness=5;
Clearance=0.5;
PTFEDiameter=4.12+Clearance;
Offsets=20;
///////////////////////////////////////////////////////////////////////////////////////////////////////////

Bracket(20); // CXY-MSv1

///////////////////////////////////////////////////////////////////////////////////////////////////////////

module Bracket(Length=40,Size=9.5) {
	difference() {
		color("cyan") cubeX([Length+Offsets,20,Thickness],2);
		translate([10,10,-2]) ExtrusionMount();
		translate([Length+Offsets-11,10,-2]) rotate([0,25,0]) cylinder(h=20,d=PTFEDiameter);
	}
	translate([Length+Offsets-10,10,0]) ConnectorMount(Size);
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////

module ConnectorMount(Size=9.5) {
	difference() {
		rotate([0,25,0]) {
			difference() {
				translate([0,0,3]) color("green") cyl(h=25,d=Size*1.5,rounding=1);
				translate([0,0,-3]) color("black") cylinder(h=20,d=Size);
			}
		}
		translate([-15,-10,-20]) color("gray") cube([25,20,20]);
		rotate([0,25,0]) translate([0,0,19]) color("gray") hull() { // bevel edge to make it easier to insert fitting
			cylinder(h=1,d=Size*1.5);
			translate([0,0,-5])cylinder(h=1,d=Size);
		}
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////

module ExtrusionMount(Screw=screw5) {
	color("red") cylinder(h=10,d=Screw);
	if(Screw==screw5) translate([0,0,6.5]) color("plum") cylinder(h=5,d=screw5hd);
	else if(Screw==screw3) translate([0,0,6.5]) color("plum") cylinder(h=5,d=screw3hd);
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////