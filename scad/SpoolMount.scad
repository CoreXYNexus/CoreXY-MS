///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SpoolMount.scad - mount a spool onto 2020
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// created: 1/27/2019
// last modified: 5/12/22
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// https://creativecommons.org/licenses/by-sa/4.0/
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// 1/27/19	- Mounting a spool to 2020, uses 4 M3x8mm screws, 4 M3 post install nuts, 200mm of 8mm threaded rod,
//			  three M8 nuts, two M8 washers, 20mm of 2020, one M5 insall nut, one M5x5mm or M5x25mm cap screw
// 11/27/20	- Added spacers for the mounting rod for the sppol, since it's longer than needed for my current spools
// 1/4/22	- BOSL2
// 1/22/22	- Better variable names
// 5/12/22	- Change mount screw holes to top
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// uses http://www.thingiverse.com/thing:1647748 to hold the spool
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
include <bosl2/std.scad>
include <inc/screwsizes.scad>
$fn=100;
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
Clearance = 0.4;
Width2020 = 20 + Clearance;
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

SpoolHolder();
//Spacer(4,25);

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module Spacer(Quanity=1,Length=50) {
	for(x = [0:Quanity-1]) {
		translate([x*15,0,0]) difference() {
			color("cyan") cyl(h=Length,d=screw8*1.5,rounding=1.5);
			color("red") cyl(h=Length+10,d=screw8+0.5);
		}
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module SpoolHolder(Length=60,MountWidth=28) {
	ExtrusionMount(Length);
	translate([0,0,-9]) RodMount(MountWidth);
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module RodMount(MountWidth=28) {
	difference() {
		color("cyan") hull() {
			translate([0,0,MountWidth]) rotate([90,0,0]) color("cyan") cyl(h=MountWidth,d=30,rounding=2);
			translate([0,0,MountWidth-10]) rotate([90,0,0]) color("cyan") cyl(h=MountWidth,d=30,rounding=2);
		}
		SpoolRodHole(screw8,MountWidth);
		translate([0,0,10]) Slot2020(MountWidth);
	}
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module SpoolRodHole(Screw=screw8,MountWidth=28) {
	translate([0,0,MountWidth]) rotate([90,0,0]) color("blue") cyl(h=MountWidth+5,d=Screw);
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


module ExtrusionMount(Length=60,MountWidth=28) {
	difference() {
		color("lightgrey") cuboid([Length,MountWidth,MountWidth],rounding=2); 
		Slot2020();
		translate([-21,0,15]) MountHoles2020();
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module MountHoles2020(Screw=screw5,ScrewHD=screw5hd) {
	color("plum") cyl(h=20,d=Screw);
	translate([42,0,0]) color("yellow") cyl(h=20,d=Screw);
	color("yellow") cyl(h=5,d=ScrewHD);
	translate([42,0,0]) color("plum") cyl(h=5,d=ScrewHD);
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module Slot2020(Length=60,MountWidth=28) {
	translate([0,(MountWidth-Width2020)/2-3.5,-3]) color("red") cuboid([Length+5,Width2020,25],rounding=1);
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
